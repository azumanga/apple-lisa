LTVAL           .EQU    0
GTVAL           .EQU    1
EQVAL           .EQU    2


        .FUNC STGCOMP,12

;------------------------------------------------------------------------
;       FUNCTION STGCOMP(PS1
;                      PS2     : PTRDATA;
;                      LEN1
;                      LEN2    : INTEGER) : INTEGER;
;
;
;
;       USES
;               A0,A1
;               D0,D1,D2,D3
;
;       SAVES
;               D2,D3
;
LEN2    .EQU    12
LEN1    .EQU    14
PS2     .EQU    16
PS1     .EQU    20
NPARMS  .EQU    12

        MOVEM.L D2-D3,-(SP)                     ;SAVE REGISTERS
        MOVE.W  LEN1(SP),D0                     ;D0 := LEN1
        MOVE.W  LEN2(SP),D1                     ;D1 := LEN2
        MOVE.L  PS1(SP),A0                      ;A0 := PS1
        MOVE.L  PS2(SP),A1                      ;A1 := PS2

        CMP.W   D1,D0                           ;COMPARE FOR SHORTER LENGTH
        MOVE    SR,D3                           ;SAVE CCR
        BLS.S   @1
        MOVE.W  D1,D0
@1      TST     D0
        BEQ.S   COMPEQ                          ;TRAP 0 LENGTH STRINGS

STRCOMP MOVE.B  (A0)+,D1                        ;D1 IS UPSHIFT OF (A1)
        CMPI.B  #96,D1
        BLS.S   @1
        CMPI.B  #122,D1
        BHI.S   @1
        SUBI    #32,D1
@1      MOVE.B  (A1)+,D2                        ;D2 IS UPSHIFT OF (A2)
        CMPI.B  #96,D2
        BLS.S   @2
        CMPI.B  #122,D2
        BHI.S   @2
        SUBI    #32,D2
@2      CMP.B   D2,D1                           ;IF UPSHIFT(A1) = UPSHIFT (A2)
        BEQ.S   CONTINU                         ;THEN CONTINUE
                                                ;ELSE IF > RETURN GTVAL
                                                ;ELSE IF < RETURN LTVAL
COMPNE  BHI.S   RETGT
        MOVEQ   #LTVAL,D0
        BRA.S   RETURN
RETGT   MOVEQ   #GTVAL,D0

RETURN  MOVEM.L (SP)+,D2-D3                     ;RESTORE REGISTERS
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDRESS
        MOVEQ   #NPARMS,D1
        ADDA.L  D1,SP
        MOVE.W  D0,(SP)                         ;STORE RETURN VALUE
        JMP     (A0)

CONTINU SUBQ.W  #1,D0                           ;IF --CNT <> -1 GOTO CMPLOOP
        BNE.S   STRCOMP

COMPEQ  MOVE    D3,CCR                          ;INCONCLUSIVE - LONGEST WINS
        BNE.S   COMPNE
        MOVEQ   #EQVAL,D0
        BRA.S   RETURN


        .END

ÿ