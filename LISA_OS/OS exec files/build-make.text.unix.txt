$EXEC {'make'--compile and link a program...

       %0 -- name of the program source file
       %1 -- name for the (final) program object file
       %2 -- (optional) name of the linklist.   The default linklist is:
               %0  (ie SOURCE/myprogram)
               object/iospaslib
       %3 -- (optional) name of the post-compile, pre-linked object for the
          program, IF you want to save it.  If %3 isn't specified, then
          1) the temp file is removed after linking, 2) the user supplied
          linklist should include %0.  If %3 is given, it must be different
          than %1 }
$IF %0='' THEN
   $WRITE 'SOURCE FILE TO MAKE? '
   $READLN %0
   $IF %3='' THEN
      $WRITELN 'NAME FOR THE PRE-LINKED OBJECT FILE'
      $WRITE '         [<CR> TO DELETE AFTER LINK] [.OBJ]? '
      $READLN %3
   $ENDIF
   $IF %1='' THEN
      $WRITE "NAME FOR PROGRAM'S (FINAL) OBJECT FILE ? "
      $READLN %1
   $ENDIF
   $IF %2='' THEN
      $WRITE "NAME OF LINKLIST [<CR> FOR %0 AND object/iospaslib ONLY]? "
      $READLN %2
   $ENDIF
$ENDIF
$IF %3=%1 THEN
   $WRITELN
   $WRITELN 'THE FINAL PROGRAM NAME CAN''T BE THE SAME AS THE PRE-LINKED OBJECT.'
   $WRITELN
   $WRITELN 'NAME FOR THE PRE-LINKED OBJECT FILE'
   $WRITE '         [<CR> TO DELETE AFTER LINK] [.OBJ]? '
   $READLN %3
   $WRITE "NAME FOR PROGRAM'S (FINAL) OBJECT FILE ? "
   $READLN %1
   $IF %3=%1 THEN
      $SET %3 TO ''
   $ENDIF
$ENDIF
$SET %4 TO %3
$IF %4='' THEN
   $SET %4 TO %0
$ENDIF
$SET %9 to CONCAT(%4,'.obj') {post-compile, but pre-linked pathname}
$SET %8 to CONCAT(%1,'.obj') {linked pathname}
$SUBMIT build/comp(%0,%4)    {compile the program}
$IF %2='' THEN               {no explicit linklist}
   L{ink}%4
   object/iospaslib
   {end of object list}
   {no listing}
   %8
$ELSE
   L{ink}<%2
   {no listing}
   %8
$ENDIF
$IF %3='' THEN
   F{ilemgr}D{elete}%9  {delete pre-link object}
   Y{es, delete it}
$ELSE
   F{ilemgr}
$ENDIF
R{ename}%8,%1           {give final name to linked program}
Y{es, rename it}Q{uit}
$ENDEXEC
ÿ