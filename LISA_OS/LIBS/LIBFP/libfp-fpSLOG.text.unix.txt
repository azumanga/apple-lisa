;-----------------------------------------------------------
; 28DEC82: BUILT FROM SQRT BY JEROME COONEN
; 29APR83: CLASS ADDED (JTC)
; 09JUN83: PRESERVE A5,A6 (JTC)
;
;-----------------------------------------------------------
LOGBTOP:
        CLR.B   D6              ; SIGN IS IRRELEVANT

        CMPI.W  #2,D3           ; IS THE OPERAND +-0?
        BNE.S   @1

;-----------------------------------------------------------
; LOGB(+-0) --> DIV BY ZERO --> ERROR BIT, STUFF INF, RET.
;       BSET    #ERRZ+8,D6
;       BSET    #7,D6           ; SIGN OF MINUS INF
;-----------------------------------------------------------
        ORI.W   #$0880,D6       ; POOR MAN'S BSET'S
        MOVEA.W #$7FFF,A4       ; BIG EXP
        CLR.L   D4              ; ZERO DIGS
        MOVE.L  D4,D5
        RTS

@1:
        CMPI.W  #4,D3           ; IS THE OPERAND +-INF?
        BNE.S   @10

;-----------------------------------------------------------
; LOGB(+-INF) --> +INF --> RET.
;-----------------------------------------------------------
        RTS


;-----------------------------------------------------------
; LOGB(FINITE, NONZERO) --> EXPONENT, NORMALIZED AS A
; FLOATING-POINT NUMBER.  NEVER EXCEPTIONAL, BUT PASS
; THROUGH COERCE TO NORMALIZE AND TEST FOR ZERO.
;-----------------------------------------------------------
@10:
        CLR.L   D5              ; CLEAR THE SIGNIFICANT BITS
        SUBA.W  #$3FFF,A4       ; UNBIAS EXPONENT
        MOVE.L  A4,D4           ; MOVE AS INTEGER
        BPL.S   @12

        ORI.B   #$80,D6         ; SET SIGN
        NEG.L   D4              ; MAGNITUDE OF VALUE
@12:
        MOVEA.W #$401E,A4       ; EXPONENT = 31, BIASED
        BRA     ZNORMCOERCE



;-----------------------------------------------------------
; SCALB BEHAVES MUCH LIKE LOGB, EXCEPT THAT THE INTEGER
; ARGUMENT MUST BE PULL FROM ITS SOURCE LOCATION, IT IS
; MORE CONVENIENT NOT TO UNPACK THE INPUT INTEGER TO
; FLOATING-POINT FORM.  COUNT ON INTEGER'S ADDRESS IN
; LKADR2(A6).
; EASY CASES -- SCALB(N, ZERO/INF/NAN) --> ZERO/INF/NAN.
;-----------------------------------------------------------
SCALBTOP:
        TST.W   D3              ; IS THE OPERAND +-0, +-INF?
        BEQ.S   @1
        RTS

;-----------------------------------------------------------
; JUST ADD THE INTEGER ADJUSTMENT INTO THE EXPONENT IN A4,
; AND CHECK FOR OVER/UNDERFLOW.
;-----------------------------------------------------------
@1:
        MOVEA.L LKADR2(A6),A3   ; SRC ADDRESS
        ADDA.W  (A3),A4
        BRA     COERCE



;-----------------------------------------------------------
; CLASS PLACES INTEGER CODE AT DST ADDRESS.  THE CODE TIES
; IN USEFULLY WITH THE PASCAL ENUMERATED TYPES IN SANE.
; IT IS THE SANE VALUE PLUS ONE, WITH THE SIGN OF THE INPUT
; OPERAND.  IN SANE, THE SIGN IS PLACED IN A SEPARATE INT.
; THE VALUES ARE THUS:
;       SNAN    1
;       QNAN    2
;       INF     3
;       ZERO    4
;       NORMAL  5
;       DENORM  6
;-----------------------------------------------------------
CLASSTOP:
        MOVEQ   #5,D0           ; ASSUME NORMAL NUMBER
        TST.L   D3              ; CHECK FOR DENORM
        BMI.S   @99
        BEQ.S   CLASSFIN

        SUBQ.W  #2,D0           ; ASSUME INF
        CMPI.W  #4,D3           ; INF CODE
        BEQ.S   CLASSFIN
@99:
        ADDQ.W  #1,D0
CLASSFIN:
        TST.B   D6              ; NONZERO -> NEGATIVE
        BEQ.S   @100

        NEG.W   D0
@100:
        MOVEA.L LKADR1(A6),A3
        MOVE.W  D0,(A3)
        RTS

ÿ