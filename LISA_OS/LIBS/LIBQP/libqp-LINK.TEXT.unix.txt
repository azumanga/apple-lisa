$EXEC{LINK.TEXT}   {R<LINK(SAMPLENAME,TOOLNUMBER,OFFICEDISKNAME,UNITA,UNITB,...)}
$
${----DEFAULT SAMPLENAME TO SAMPLE, TOOLNUMBER TO 6, AND OFFICEDISKNAME TO PREFIX VOLUME----}
$DEFAULT %0 TO 'SAMPLE'
$DEFAULT %1 TO '20'
$IF (%2<>'') AND (Exists("-%2")) THEN
$  SET %2 TO "-%2-"
$ENDIF
$DEFAULT %3 TO '-'
$DEFAULT %4 TO '-'
$DEFAULT %5 TO '-'
$DEFAULT %6 TO '-'
$
$
${----CONVERT THE PHRASE FILE, IF NECESSARY----}
$IF %1 <> '-' THEN
$  SET %9 TO <NeedToDo(%2~{T%1~}PHRASE,P%0.TEXT,PABC.TEXT)
$  IF %9='T' THEN
$  WRITELN "Need to generate %2{T%1}PHRASE"
RTKALERT
P%0
%2~{T%1~}
$  ENDIF
$ENDIF
$
$IF Exists('TKLIB.OBJ') THEN
$  SET %9 TO <NeedToDo(%2~{T%1~}OBJ,TKLIB.OBJ,U%0.OBJ,M%0.OBJ)
$ELSE
$  SET %9 TO <NeedToDo(%2~{T%1~}OBJ,XFER.OBJ,UFONT.OBJ,UOBJECT.OBJ,UDRAW.OBJ,UABC.OBJ,{
                    }U%0.OBJ,M%0.OBJ)
$ENDIF
$IF %9='F' THEN
    $SET %9 TO <NeedToDo(%2~{T%1~}OBJ,U%3.OBJ,U%4.OBJ,U%5.OBJ,U%6.OBJ)
$ENDIF
$IF %9='T' THEN
$ {----DELETE OLD OBJ FILE BEFORE LINK----}
F{iler}D{elete}%2~{T%1~}OBJ
Y{es}Q{uit Filer}
$ {----KILL COMPILER AND CODE GENERATOR PROCESSES TO MAKE MORE ROOM----}
S{ysMgr}M{ManageProcesses}K{ill Process}PASCAL
K{ill}CODE
Q{uit ManageProcesses}Q{uit SysMgr}
$ {----LINK----}
L{ink}?
+W
INTRINSIC.LIB

$IF Exists('TKLIB.OBJ') THEN
TKLIB
$ELSE
CLASLIB
XFER
UOBJECT
UDRAW
UABC
$ENDIF
$IF %3<>'-' THEN
U%3
$ENDIF
$IF %4<>'-' THEN
U%4
$ENDIF
$IF %5<>'-' THEN
U%5
$ENDIF
$IF %6<>'-' THEN
U%6
$ENDIF
U%0
M%0
SYS1LIB
$IF Exists('SUOBJ.OBJ') THEN
SUOBJ
$ELSE
SuLib
$ENDIF
IOSPASLIB
IOSFPLIB
{NO MORE FILES}
{NO LIST FILE}
%2~{T%1~}
$ {----RENAME OBJ FILE AFTER LINK----}
F{iler}R{ename}%2~{T%1~}.OBJ
~{T%1~}OBJ
D{elete}MAKE..TEXT
Y{es}Q{uit Filer}
$ENDIF
$ENDEXEC

ÿ