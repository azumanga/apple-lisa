
          ;_______________________________________________________________________
          ;|                                                                      |
          ;|                                                                      |
          ;|            Queue68K -- Assembly code for Queue Package               |
          ;|                                                                      |
          ;|______________________________________________________________________|

 ;{------------------------------------------------------------------------------------------}
 ;{                             Copyright 1984, Apple Computer Inc.                          }
 ;{------------------------------------------------------------------------------------------}


          .seg      'Pr'

;PROCEDURE QueueInUse(Queue:TH);
          .PROC     QueueInUse
          .REF      YIELD_CPU

QueueIU   move.l    4(sp),a0                      ;get queue handle
          move.l    (a0),a0                       ;get queue
          bclr      #7,(a0)                       ;test most significant bit
          beq       yield                         ;in use, let others run
          move.l    (sp)+,a0                      ;get return address
          cmp.l     (sp)+,d0                      ;pop off parameter
          jmp       (a0)                          ;return

yield     move.w    #0,-(sp)                      ;push an error
          move.l    sp,-(sp)                      ;push address of error
          move.b    #1,-(sp)                      ;anyone may run (TRUE)
          jsr       YIELD_CPU                     ;yield to others
          cmp.w     (sp)+,d0                      ;pop off error
          bra.s     QueueIU                       ;jump back and test again

;PROCEDURE QueueFree(Queue:hQue);
          .PROC     QueueFree
          move.l    (sp)+,a0                      ;get return address
          move.l    (sp)+,a1                      ;get queue handle
          move.l    (a1),a1                       ;get queue
          bset      #7,(a1)                       ;set it free
          jmp       (a0)                          ;return

          .END
ÿ