; File: PASTRING.TEXT
; Date: 09-Oct-81

;        .PROC   %%%TRING

;        .DEF    %_CAT,%_POS,%_COPY,%_DEL,%_INS

;
; %_CAT - Concatenate strings
;
; Parameters: ST.L - Address of 1st string
;             ST.L - Address of 2nd string
;             ...
;             ST.L - Address of Nth string
;             ST.L - Address to put result
;             ST.W - N
;
; This routine preserves all registers
;

        .PROC   %_CAT

        MOVEM.L D0-D2/A0-A2,-(SP)
        MOVE.W  28(SP),D0       ; N
        MOVE.L  30(SP),A0       ; Address of result
        LEA     34(SP),A1       ; Addr of Addr of Nth parameter
        MOVE.W  D0,D2           ; Incr A1 to point to ...
        LSL.W   #2,D2           ; ... the address of ...
        ADDA.W  D2,A1           ; ... the first parameter
        CLR.W   D1              ; Length of result
        ADDQ.L  #1,A0
        BRA.S   C.TEST
C.LOOP: MOVE.L  -(A1),A2        ; Address of Kth string
        tst.b   (a2)            ; make resident in case it is in code seg
        CLR.W   D2
        MOVE.B  (A2)+,D2        ; Length of Kth string
        ADD.W   D2,D1           ; Update total length
        BRA.S   C.TEST2
C.LOOP2:MOVE.B  (A2)+,(A0)+
C.TEST2:dbra    D2,C.LOOP2      ; dec d2 and branch if d2 <> -1
C.TEST: dbra    D0,C.LOOP       ; See if any more strings
        MOVE.L  30(SP),A0       ; Address of result
        MOVE.B  D1,(A0)         ; Store length of result
        LEA     34(SP),A1       ; Addr of Addr of Nth parameter
        MOVE.W  28(SP),D0       ; N
        LSL.W   #2,D0           ; Set A1 to the address of ...
        ADDA.W  D0,A1           ; ... the first parameter
        MOVE.L  24(SP),-(A1)    ; Set up return address
        MOVE.L  A1,24(SP)       ; Set up final SP
        MOVEM.L (SP)+,D0-D2/A0-A2
        MOVE.L  (SP)+,SP
        RTS

;
; %_POS - Position of one string in another
;
; Parameters: ST.L - Address of SubString
;             ST.L - Address of Main String
;
; Returns:    ST.W - Position
;
; This procedure preserves all registers.
;

        .PROC   %_POS

        MOVEM.L D0-D2/A0-A2,-(SP)
        MOVE.L  28(SP),A0       ; Address of Main String
        tst.b   (a0)            ; make resident in case it is in code seg
        CLR.W   D0
        MOVE.B  (A0)+,D0        ; Length of Main String
        CLR.W   D1              ; Result
POS.TOP:MOVE.L  32(SP),A1       ; Address of SubString
        tst.b   (a1)            ; make resident in case it is in code seg
        CLR.W   D2
        MOVE.B  (A1)+,D2        ; Length of Substring
        CMP.W   D2,D0           ; Compare lengths
        BGE.S   POS.TRY
        CLR.W   D1              ; No match if substring shorter
        BRA.S   POSDONE
POS.TRY:MOVE.L  A0,A2           ; Get fresh copy of main pointer
        ADDQ.L  #1,D1           ; Update result counter
        BRA.S   POSTEST
POSLOOP:CMPM.B  (A2)+,(A1)+
        BNE.S   TRYNEXT         ; No Match if not equal
POSTEST:dbra    D2,POSLOOP      ; dec d2 and branch if d2 <> -1
        BRA.S   POSDONE         ; Fall through means found it
TRYNEXT:ADDQ.L  #1,A0           ; Update main pointer for next try
        SUBQ.W  #1,D0           ; Decrement remaining length of main string
        BRA.S   POS.TOP
POSDONE:MOVE.W  D1,34(SP)       ; Store the result
        MOVE.L  24(SP),30(SP)   ; Set up the return address
        MOVEM.L (SP)+,D0-D2/A0-A2
        ADDQ.L  #6,SP
        RTS

;
; %_COPY - Copy a substring
;
; Parameters: ST.L - Source string address
;             ST.W - Starting index
;             ST.W - Size to copy
;             ST.L - Address of result
;
; This routine saves all registers
;

        .PROC   %_COPY

        MOVEM.L D0-D2/A0-A1,-(SP)
        MOVE.L  24(SP),A0       ; Address for result
        MOVE.W  28(SP),D0       ; Size to copy
        ble.s   y.error
        cmpi.w   #255,d0
        bgt.s   y.error
        MOVE.W  30(SP),D1       ; Index
        ble.s   y.error
        cmpi.w   #255,d1
        bgt.s   y.error
        SUBQ.W  #1,D1
        MOVE.L  32(SP),A1       ; Address of source string
        tst.b   (a1)            ; make resident in case it is in code seg
        CLR.W   D2              ; to get length of source
        MOVE.B  (A1)+,D2        ;
        SUB.W   D1,D2
        SUB.W   D0,D2
        BLT.S   Y.ERROR         ; Error if too little source
        ADDA.W  D1,A1           ; Point to first byte to copy
        MOVE.B  D0,(A0)+        ; Store result length
        BRA.S   Y.TEST
Y.LOOP: MOVE.B  (A1)+,(A0)+
Y.TEST: dbra    D0,Y.LOOP       ; dec d0 and branch if d0 <> -1
Y.LEAVE:MOVE.L  20(SP),32(SP)   ; Set up return address
        MOVEM.L (SP)+,D0-D2/A0-A1
        ADD.L   #12,SP
        RTS
Y.ERROR:CLR.B   (A0)            ; Set result to null for now
        BRA.S   Y.LEAVE         ; And return

;
; %_DEL - Delete a substring form a string
;
; Parameters: ST.L - Address of string
;             ST.W - Position to start deleting
;             ST.W - Number bytes to delete
;

        .PROC   %_DEL

        MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D0        ; Pop Number bytes
        MOVE.W  (SP)+,D1        ; Pop position
        MOVE.L  (SP)+,A0        ; Pop string address
        CMPI.W  #0,D0           ; Exit if bytes to delete
        BLE.S   D.DONE          ; is <= 0
        CMPI.W  #0,D1           ; or if starting position
        BLE.S   D.DONE          ; is <= 0
        CLR.W   D2              ; Fetch ...
        MOVE.B  (A0),D2         ; ... string size
        CMP.W   D1,D2           ; Exit if starting position
        BLT.S   D.DONE          ; is > string size
        MOVE.W  D1,D3           ; Compare position
        ADD.W   D0,D3           ; plus number bytes
        SUBQ.W  #1,D3           ; minus 1
        SUB.W   D2,D3           ; to string size
        BLE.S   D.SETUP
        SUBQ.W  #1,D1           ; Compute result size
        MOVE.B  D1,(A0)         ; and store it
        BRA.S   D.DONE
D.SETUP:SUB.W   D0,D2           ; Compute result size
        MOVE.B  D2,(A0)         ; and store it
        ADDA.W  D1,A0           ; Point to first char to delete
        MOVE.L  A0,A1
        ADDA.W  D0,A1           ; Point to first char to move
        BRA.S   D.TEST
D.LOOP: MOVE.B  (A1)+,(A0)+
D.TEST: ADDQ.W  #1,D3
        BLE.S   D.LOOP
D.DONE: JMP     (A2)

;
; %_INS - Insert a string in another
;
; Parameters: ST.L - Address of string to insert
;             ST.L - Address of main string
;             ST.W - Position in main string to insert
;
; must preserve d4-d7, a3-a7

        .PROC   %_INS

        MOVE.L  (SP)+,A2        ; Pop return address
        MOVE.W  (SP)+,D0        ; Pop index
        MOVE.L  (SP)+,A1        ; Pop address of main string
        MOVE.L  (SP)+,A0        ; Pop address of string to insert
        tst.b   (a0)            ; make resident in case it is in code seg
        MOVE.L  A2,-(SP)
        SUBQ.W  #1,D0
        BLT.S   I.DONE          ; Exit if index < 0
        CLR.W   D1
        MOVE.B  (A0)+,D1        ; Length of substring
        CLR.W   D2
        MOVE.B  (A1),D2         ; Length of Main String
        CMP.W   D0,D2           ; Exit if index
        BLT.S   I.DONE          ; not in main string
        MOVE.W  D1,D3
        ADD.W   D2,D3           ; Final string size
        MOVE.B  D3,(A1)+
        MOVE.L  A1,A2
        ADDA.W  D3,A2           ; points to last byte in result string +1
        ADDA.W  D2,A1           ; points to last byte of main string +1
        SUB.W   D0,D2           ; How many bytes to move
        BRA.S   I.TEST
I.LOOP: MOVE.B  -(A1),-(A2)
I.TEST: dbra    D2,I.LOOP       ; dec d2 and branch if d2 <> -1
        BRA.S   I.TEST2
I.LOOP2:MOVE.B  (A0)+,(A1)+     ; A1 is Address of hole
I.TEST2:dbra    D1,I.LOOP2      ; dec d1 and branch if d1 <> -1
I.DONE: RTS

        .END

ÿ