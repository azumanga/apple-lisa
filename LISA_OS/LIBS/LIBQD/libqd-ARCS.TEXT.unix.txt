
        .INCLUDE  libqd/GRAFTYPES.TEXT
;-----------------------------------------------------------
;
;
;       *    *****    ***    ***
;      * *   *    *  *   *  *   *
;     *   *  *    *  *      *
;     *   *  *****   *       ***
;     *****  *  *    *          *
;     *   *  *   *   *   *  *   *
;     *   *  *    *   ***    ***
;
;   Procedures for drawing Arcs:
;


        .PROC StdArc,4
        .REF  CheckPic,PutPicVerb,PutPicWord,PutPicRect
        .REF  PushVerb,DrawArc
;---------------------------------------------------------------
;
;  PROCEDURE StdArc(verb: GrafVerb; r: Rect; startAngle,arcAngle: INTEGER);
;
;  A6 OFFSETS OF PARAMS AFTER LINK:
;
PARAMSIZE       .EQU    10
VERB            .EQU    PARAMSIZE+8-2           ;GRAFVERB
RECT            .EQU    VERB-4                  ;LONG, ADDR OF RECT
STARTANG        .EQU    RECT-2                  ;WORD
ARCANG          .EQU    STARTANG-2              ;WORD

OVWD            .EQU    -2                      ;WORD
OVHT            .EQU    OVWD-2                  ;WORD
VARSIZE         .EQU    OVHT                    ;TOTAL BYTES OF LOCALS


        LINK    A6,#VARSIZE                     ;ALLOCATE STACK FRAME
        MOVEM.L D4/D7/A3-A4,-(SP)               ;SAVE REGS
        MOVE.B  VERB(A6),D7                     ;GET VERB
        JSR     CHECKPIC                        ;SET UP A4,A3 AND CHECK PICSAVE
        BLE     NOTPIC                          ;BRANCH IF NOT PICSAVE

        MOVE.B  D7,-(SP)
        JSR     PutPicVerb                      ;PUT ADDIONAL PARAMS TO THEPIC
        MOVEQ   #$60,D0                         ;PUT ARCNOUN IN HI NIBBLE
        ADD     D7,D0                           ;PUT VERB IN LO NIBBLE
        MOVE.B  D0,-(SP)                        ;PUSH OPCODE
        MOVE.L  RECT(A6),-(SP)                  ;PUSH ADDR OF RECT
        JSR     PutPicRect                      ;PUT OPCODE AND RECTANGLE
        MOVE    STARTANG(A6),-(SP)
        JSR     PutPicWord                      ;PUT STARTANGLE
        MOVE    ARCANG(A6),-(SP)
        JSR     PutPicWord                      ;PUT ARCANGLE

NOTPIC  MOVE.L  RECT(A6),A0                     ;POINT TO RECT
        MOVE    RIGHT(A0),D0
        SUB     LEFT(A0),D0
        MOVE    D0,OVWD(A6)                     ;OVWD := R.RIGHT - R.LEFT
        MOVE    BOTTOM(A0),D0
        SUB     TOP(A0),D0
        MOVE    D0,OVHT(A6)                     ;OVHT := R.BOTTOM - R.TOP

        MOVE.L  A0,-(SP)                        ;PUSH ADDR OF RECT
        TST.B   D7                              ;IS VERB FRAME ?
        BNE.S   NOTFR                           ;NO, CONTINUE
;
;  Currently, FrameArc does not put inversion points to theRgn.
;  If this changes, add test and call to PutArc here.
;
        MOVE.B  #1,-(SP)                        ;PUSH HOLLOW = TRUE
        BRA.S   DOIT
NOTFR   CLR.B   -(SP)                           ;PUSH HOLLOW = FALSE
DOIT    MOVE.L  OVHT(A6),-(SP)                  ;PUSH OVWD,OVHT
        JSR     PushVerb                        ;PUSH MODE AND PATTERN
        MOVE    STARTANG(A6),-(SP)              ;PUSH STARTANGLE
        MOVE    ARCANG(A6),-(SP)                ;PUSH ARCANGLE

;  DrawArc(r,hollow,ovWd,ovHt,mode,pat,startAng,arcAng);

        JSR     DrawArc
        MOVEM.L (SP)+,D4/D7/A3-A4               ;RESTORE REGS
        UNLINK  PARAMSIZE,'STDARC  '



        .PROC CallArc,3
        .REF  StdArc
;---------------------------------------------------------------
;
;  PROCEDURE CallArc(r: Rect; startAngle,arcAngle: INTEGER);
;
;  code shared by FrameArc, PaintArc, EraseArc, InvertArc, and FillArc.
;  enter with verb in D0.
;
        FAKEHEAD
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        MOVE.L  (SP)+,D1                        ;POP BOTH ANGLES
        MOVE.L  (SP)+,A1                        ;POP ADDR OF RECT
        MOVE.B  D0,-(SP)                        ;PUSH VERB
        MOVE.L  A1,-(SP)                        ;PUSH ADDR OF RECT
        MOVE.L  D1,-(SP)                        ;PUSH BOTH ANGLES
        MOVE.L  A0,-(SP)                        ;PUSH RETURN ADDR
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO LISAGRAF GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;GET CURRENT GRAFPORT
        MOVE.L  GRAFPROCS(A0),D0                ;IS GRAFPROCS NIL ?
        LEA     STDARC,A0
        BEQ.S   USESTD                          ;YES, USE STD PROC
        MOVE.L  D0,A0
        MOVE.L  ARCPROC(A0),A0                  ;NO, GET PROC PTR
USESTD  JMP     (A0)                            ;GO TO IT
        FAKETAIL 'CALLARC '


        .PROC FrameArc,3
        .REF  CallArc
;-----------------------------------------------------
;
;  PROCEDURE FrameArc(* r: Rect; startAngle,arcAngle: INTEGER *);
;
        FAKEHEAD
        MOVEQ   #FRAME,D0                       ;VERB = FRAME
        JMP     CallArc                         ;SHARE COMMON CODE
        FAKETAIL 'FRAMEARC'


        .PROC PaintArc,3
        .REF  CallArc
;-----------------------------------------------------
;
;  PROCEDURE PaintArc(* r: Rect; startAngle,arcAngle: INTEGER *);
;
        FAKEHEAD
        MOVEQ   #PAINT,D0                       ;VERB = PAINT
        JMP     CallArc                         ;SHARE COMMON CODE
        FAKETAIL 'PAINTARC'


        .PROC EraseArc,3
        .REF  CallArc
;--------------------------------------------------------
;
;  PROCEDURE EraseArc(* r: Rect; startAngle,arcAngle: INTEGER *);
;
        FAKEHEAD
        MOVEQ   #ERASE,D0                       ;VERB = ERASE
        JMP     CallArc                         ;SHARE COMMON CODE
        FAKETAIL 'ERASEARC'


        .PROC InvertArc,3
        .REF  CallArc
;--------------------------------------------------------
;
;  PROCEDURE InvertArc(* r: Rect; startAngle,arcAngle: INTEGER *);
;
        FAKEHEAD
        MOVEQ   #INVERT,D0                      ;VERB = INVERT
        JMP     CallArc                         ;SHARE COMMON CODE
        FAKETAIL 'INVERTAR'


        .PROC FillArc,4
        .REF  CallArc
;--------------------------------------------------------
;
;  PROCEDURE FillArc(* r: Rect; startAngle,arcAngle: INTEGER; pat: Pattern *);
;
        FAKEHEAD
        MOVE.L  (SP)+,A0                        ;POP RETURN ADDR
        MOVE.L  (SP)+,A1                        ;POP ADDR OF PATTERN
        MOVE.L  A0,-(SP)                        ;PUT RETURN ADDR BACK
        MOVE.L  GRAFGLOBALS(A5),A0              ;POINT TO LISAGRAF GLOBALS
        MOVE.L  THEPORT(A0),A0                  ;GET CURRENT GRAFPORT
        LEA     FILLPAT(A0),A0                  ;POINT TO FILLPAT
        MOVE.L  (A1)+,(A0)+                     ;COPY PAT INTO FILLPAT
        MOVE.L  (A1)+,(A0)+                     ;ALL EIGHT BYTES
        MOVEQ   #FILL,D0                        ;VERB = FILL
        JMP     CallArc                         ;SHARE COMMON CODE
        FAKETAIL 'FILLARC '



        .END




ÿ