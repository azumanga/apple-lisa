
;-----------------------------------------------------------
;
;  --> FASTLINE.TEXT
;
;  Continuation of DrawLine
;

;--------------------------------------------------------
;
;  IF PATTERN WAS WHITE, ALTER PEN MODE AS IF IT WERE BLACK
;  QUIT IF DO-NOTHING MODE.
;
FASTLINE MOVE   MODE(A6),D5                     ;GET PENMODE, 8..15
         TST    D0                              ;IS PATTERN WHITE ?
         BNE.S  PATOK                           ;NO, CONTINUE
         EOR    #4,D5                           ;YES, ALTER MODE AS IF BLACK
PATOK    CMP    #12,D5                          ;IS ALTERED MODE DO-NOTHING ?
         BGT    DONE                            ;YES, RESTORE CURSOR AND QUIT


;-------------------------------------------------------
;
;  IS THE LINE HORIZONTAL ? IF SO DRAW IT AND QUIT.
;
        CMP     D1,D3                           ;IS LINE HORIZONTAL ?
        BNE     VLINE                           ;NO, MUST BE VERTICAL
        BSR     SETUP                           ;GET DSTPTR,ROWBYTES,HGLOBAL
        JSR     LEFTMASK                        ;GET LEFT MASK
        MOVE    D0,D6                           ;PUT IT IN D6
        MOVE    MINRECT+RIGHT(A6),D2            ;GET MINRECT RIGHT
        SUB     PORTBITS+BOUNDS+LEFT(A5),D2     ;CONVERT TO GLOBAL
        MOVE    D2,D0                           ;MAKE A COPY
        JSR     RIGHTMASK                       ;GET RIGHT MASK
        MOVE    D0,D3                           ;PUT IT IN D3
        LSR     #4,D2                           ;CONVERT DOTS TO WORDS

        CMP     #10,D5                          ;WHICH MODIFIED MODE ?
        BLT.S   HSET                            ;BR IF MODE 8 OR 9 (SET)
        BEQ.S   HTOGL                           ;BR IF MODE 10 (XOR)
                                                ;ELSE MODE 11 OR 12 (CLR)

;------------------------------------------------------
;
;  DRAW A WHITE HORIZONTAL LINE
;
        NOT     D6                              ;MAKE NOT LEFTMASK
        NOT     D3                              ;MAKE NOT RIGHTMASK
        SUB     D1,D2                           ;CALC WORDCOUNT
        BEQ.S   ENDCLR                          ;BR IF ALL IN ONE WORD
        AND     D6,(A4)+                        ;DO LEFT WORD WITH MASK
        CLR.L   D6                              ;FLUSH LEFTMASK FOR END
        SUB     #1,D2                           ;DEC WORD COUNT
        BEQ.S   ENDCLR                          ;BR IF NO UNMASKED WORDS
        LSR     #1,D2                           ;HALVE WORDCOUNT FOR LONGCOUNT
        BCC.S   CLRONE                          ;BR IF EVEN # WORDS LEFT
        MOVE    D6,(A4)+                        ;ELSE MAKE EVEN BY DOING A WORD
        SUB     #1,D2                           ;ADJUST LONGCOUNT
        BRA.S   CLRMORE                         ;SEE IF ANY LONGS LEFT TO DO
CLRTWO  MOVE.L  D6,(A4)+                        ;FILL A LONG WITH BLACK
CLRONE  MOVE.L  D6,(A4)+                        ;FILL ANOTHER LONG
        SUB     #2,D2                           ;ANY UNMASKED LONGS LEFT ?
CLRMORE BGT     CLRTWO                          ;YES, AT LEAST TWO LONGS
        BEQ     CLRONE                          ;YES, FINISH UP LAST LONG
ENDCLR  OR      D3,D6                           ;COMBINE LEFT AND RIGHT MASK
        AND     D6,(A4)                         ;DRAW LINE
        BRA     DONE                            ;RESTORE CURSOR AND QUIT


;--------------------------------------------------
;
;  DRAW AN XOR HORIZONTAL LINE
;
HTOGL   SUB     D1,D2                           ;CALC WORDCOUNT
        BEQ.S   ENDTOGL                         ;BR IF ALL IN ONE WORD
        EOR     D6,(A4)+                        ;DO LEFT WORD WITH MASK
        MOVEQ   #-1,D6                          ;FLUSH LEFTMASK FOR END
        SUB     #1,D2                           ;DEC WORD COUNT
        BEQ.S   ENDTOGL                         ;BR IF NO UNMASKED WORDS
        LSR     #1,D2                           ;HALVE WORDCOUNT FOR LONGCOUNT
        BCC.S   TOGLONE                         ;BR IF EVEN # WORDS LEFT
        EOR     D6,(A4)+                        ;ELSE MAKE EVEN BY DOING A WORD
        SUB     #1,D2                           ;ADJUST LONGCOUNT
        BRA.S   TOGLMORE                        ;SEE IF ANY LONGS LEFT TO DO
TOGLTWO NOT.L   (A4)+                           ;INVERT A LONG
TOGLONE NOT.L   (A4)+                           ;INVERT ANOTHER LONG
        SUB     #2,D2                           ;ANY UNMASKED LONGS LEFT ?
TOGLMORE BGT    TOGLTWO                         ;YES, AT LEAST TWO LONGS
        BEQ     TOGLONE                         ;YES, FINISH UP LAST LONG
ENDTOGL AND     D3,D6                           ;COMBINE LEFT AND RIGHT MASK
        EOR     D6,(A4)                         ;DRAW LINE
        BRA     DONE                            ;RESTORE CURSOR AND QUIT


;--------------------------------------------
;
;  DRAW A BLACK HORIZONTAL LINE
;
HSET    SUB     D1,D2                           ;CALC WORDCOUNT
        BEQ.S   ENDSET                          ;BR IF ALL IN ONE WORD
        OR      D6,(A4)+                        ;DO LEFT WORD WITH MASK
        MOVEQ   #-1,D6                          ;FLUSH LEFTMASK FOR END
        SUB     #1,D2                           ;DEC WORD COUNT
        BEQ.S   ENDSET                          ;BR IF NO UNMASKED WORDS
        LSR     #1,D2                           ;HALVE WORDCOUNT FOR LONGCOUNT
        BCC.S   SETONE                          ;BR IF EVEN # WORDS LEFT
        MOVE    D6,(A4)+                        ;ELSE MAKE EVEN BY DOING A WORD
        SUB     #1,D2                           ;ADJUST LONGCOUNT
        BRA.S   SETMORE                         ;SEE IF ANY LONGS LEFT TO DO
SETTWO  MOVE.L  D6,(A4)+                        ;FILL A LONG WITH BLACK
SETONE  MOVE.L  D6,(A4)+                        ;FILL ANOTHER LONG
        SUB     #2,D2                           ;ANY UNMASKED LONGS LEFT ?
SETMORE BGT     SETTWO                          ;YES, AT LEAST TWO LONGS
        BEQ     SETONE                          ;YES, FINISH UP LAST LONG
ENDSET  AND     D3,D6                           ;COMBINE LEFT AND RIGHT MASK
        OR      D6,(A4)                         ;DRAW LINE
        BRA     DONE                            ;RESTORE CURSOR AND QUIT


;-------------------------------------------------------
;
;  DRAW A VERTICAL LINE.
;
VLINE   CMP     D2,D4                           ;IS LINE VERTICAL ?
        BSR     SETUP                           ;GET DSTPTR,ROWBYTES,HGLOBAL
        JSR     BITMASK                         ;GET A ONE-BIT MASK IN D0
        MOVE    MINRECT+BOTTOM(A6),D1           ;YES, GET BOTTOM
        SUB     MINRECT+TOP(A6),D1              ;CALC HEIGHT
        SUB     #1,D1                           ;INIT HEIGHT COUNT

        CMP     #10,D5                          ;WHICH MODIFIED MODE ?
        BLT.S   VSET                            ;BR IF MODE 8 OR 9 (SET)
        BEQ.S   VTOGL                           ;BR IF MODE 10 (XOR)

        NOT     D0                              ;ELSE MAKE NOTMASK
VCLR    AND     D0,(A4)                         ;CLEAR ONE DOT
        ADD     D7,A4                           ;BUMP TO NEXT ROW
        DBRA    D1,VCLR                         ;LOOP ALL DOTS
        BRA     DONE                            ;RESTORE CURSOR AND QUIT

VTOGL   EOR     D0,(A4)                         ;TOGGLE ONE DOT
        ADD     D7,A4                           ;BUMP TO NEXT ROW
        DBRA    D1,VTOGL                        ;LOOP ALL DOTS
        BRA     DONE                            ;RESTORE CURSOR AND QUIT

VSET    OR      D0,(A4)                         ;SET ONE DOT
        ADD     D7,A4                           ;BUMP TO NEXT ROW
        DBRA    D1,VSET                         ;LOOP ALL DOTS
        BRA     DONE                            ;RESTORE CURSOR AND QUIT



;------------------------------------------------------------------------
;
;  LOCAL ROUTINE TO SET UP POINTER TO FIRST WORD, BIT INDEX, AND ROWBYTES
;
;  INPUTS:      IN MINRECT IF ENTER AT SETUP, ELSE ENTER AT SETUP1 WITH
;               D1: STARTING VERT, LOCAL COORDS
;               D2: STARTING HORIZ, LOCAL COORDS
;
;  OUTPUTS:     D0: STARTING HORIZ, GLOBAL COORDS
;               D1: STARTING GLOBAL DIV 16
;               D7: ROWBYTES
;               A4: POINTER TO FIRST WORD
;
;  CLOBBERS:    D1
;
SETUP   MOVE    MINRECT+TOP(A6),D1              ;START VERT:=MINRECT TOP
        MOVE    MINRECT+LEFT(A6),D2             ;START HORIZ:=MINRECT LEFT

SETUP1  MOVE.L  PORTBITS+BASEADDR(A5),A4        ;GET BITMAP START
        MOVE    D1,D0                           ;GET STARTING VERT
        SUB     PORTBITS+BOUNDS+TOP(A5),D0      ;CONVERT TO GLOBAL
        MOVE    PORTBITS+ROWBYTES(A5),D7        ;GET ROWBYTES
        MULU    D7,D0                           ;MULT TOP BY ROWBYTES
        ADD.L   D0,A4                           ;ADD TO BASEADDR
        MOVE    D2,D0                           ;GET STARTING VERT
        SUB     PORTBITS+BOUNDS+LEFT(A5),D0     ;CONVERT TO GLOBAL
        MOVE    D0,D1                           ;MAKE A COPY
        LSR     #4,D1                           ;CONVERT BITS TO WORDS
        ADD     D1,A4                           ;ADD TO RUNNING TOTAL
        ADD     D1,A4                           ;TWICE FOR BYTES
        RTS




ÿ