$EXEC {apin/build, no input arguments, creates from scratch the Office System 1-5 diskettes}
      {%0 = name of source disk, %1 = name of release disk, %2 = version number, %3 = postfix}
      {The source disk is the librarian disk that contains all the libqd/xxx etc. files needed
       to build the apin/syslib.obj file.  The release disk is a valid, working Office System
       disk (such as a cloning disk) with all the Office System files needed on the Office
       System 1-5 diskettes.  The version number must correspond to the version number hard-
       coded in the apin/office.text source code for the Install program.  The postfix is used
       to desiminate between the office system/workshop/demo/install Install programs.  For
       example, to compile the Office System set of diskettes, the postfix is 'office'.  This
       directs the exec files to compile 'apin/office.text' and process the 'apin/officealert.text'
       alert file.}

$WRITE 'Insert the Apin 1 diskette and press <Return> '
$READLN %9
$IF NOT (EXISTS('-Apin 1')) THEN
   $WRITELN "Could not find the -Apin 1 diskette"
   $SUBMIT <AbortExec
   $ENDIF
$WRITE 'What is the name of the librarian disk (ie. <slot2chan1> or <sourcedisk>) ? '
$READLN %0
$IF NOT (EXISTS("-%0")) THEN
   $WRITELN "Could not find the specified disk"
   $SUBMIT <AbortExec
   $ENDIF
$WRITE 'What is the name of the release (build) disk (ie. <slot2chan2> or <temp>) ? '
$READLN %1
$IF NOT (EXISTS("-%1")) THEN
   $WRITELN "Could not find the specified disk"
   $SUBMIT <AbortExec
   $ENDIF
$WRITE 'What is the version number for the build (ie. 3.6) ? '
$READLN %2
$WRITE 'What is the postfix name for the build (ie. <office> or <workshop>) ? '
$READLN %3
$SET %9 TO UPPERCASE(%3)
$IF %9 = 'OFFICE' THEN
   $SET %9 TO 'Office System'
$ELSE
   $SET %9 TO %3
   $ENDIF
$WRITELN
$WRITELN 'The following steps will be taken:'
$WRITELN
$WRITELN "Necessary source and exec files will be copied from -Apin 1 to -%1"
$WRITELN "The apin/stubs & apin/stubs2 source files will be compiled and linked on -%1"
$WRITELN "The apin/syslib.obj and apin/intrinsic.lib files will be created on -%1"
$WRITELN "The apin/%3alert.text file will be processed"
$WRITELN "The apin/%3.text file will be compiled and linked against apin/intrinsic.lib"
$WRITELN "The %9 1 %2 diskette will be created"
$IF %9 = 'Office System' THEN
   $WRITELN "The Office System executable files on %1 will be prefixed with <p/> and packed"
   $WRITELN "The Office System 2-5 diskettes will be created"
   $ENDIF
$WRITELN
$WRITE "Is it ok to coninue (Y or N) [Y] ? "
$READLN %9
$IF (%9 = 'n') OR (%9 = 'N') THEN
   $SUBMIT <AbortExec
   $ENDIF
S{ysMgr}O{utput redirect}-%1-apin/list/make.text
Q{uit the system manager}
$SUBMIT -Apin 1-apin/copy/apinfiles(%1,%3)
R{un}<-%1-apin/build2(%0,%1,%2,%3)
$ENDEXEC

ÿ