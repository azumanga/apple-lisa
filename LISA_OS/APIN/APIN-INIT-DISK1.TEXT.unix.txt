$EXEC {apin/init/disk1, initializes the diskette, processes the alert file}
      {Arg0 = version number, arg1 = build volume name, arg2 = software postfix (ie. 'office')}

F{iler}
$SET %9 TO UPPERCASE(%2)
$IF %9 = 'OFFICE' THEN
   $SET %9 TO 'Office System'
   $ENDIF
$WRITE "Insert the diskette for %9 1 in the lower drive and press <Return> "
$READLN %4  {Just to clear off the <Return>}
$WRITE 'Do you wish to initialize or re-initialize the diskette (Y or N) [N] ? '
$READLN %5
$IF UPPERCASE(%5) = 'Y' THEN
   I{nitialize}lower{lower drive}
   $IF EXISTS('-lower') THEN
      Y{es, destroy the current OS directory}{Initialize to full size}
   $ELSE
      {Initialize to full size}
      $ENDIF
   %9 1 %0{name of the new volume, arg0 = version number}
   25{max number of files to allow on it}
   $ENDIF
R{ename}-lower
-%9 1 %0{name of new volume, just in case we didn't re-initialize}
D{elete}-lower-=
Y{es, delete all selected files}
D{elete}-lower-~{=
Y{es, delete all selected files}
{done with setting up a usable diskette, now get the new files ready for loading}
Q{uit the filer}
$WRITE 'Do you wish process the alert file (Y or N) [N] ? '
$READLN %5
$IF UPPERCASE(%5) = 'Y' THEN
   R{un}Alert
   -%1-apin/%2alert{the alert data file}
   -%1-apin/%2{file name apin/(office, demo, install)phrase is the destination file}
   $ENDIF
$ENDEXEC
ÿ