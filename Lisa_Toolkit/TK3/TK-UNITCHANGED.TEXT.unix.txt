EXEC(name,prevresult,lastobj,ctr,done) {assumes object file is name.OBJ;
                                            source files are name.TEXT, name2.TEXT, ...
        lastobj is the name of the previous .OBJ file
        prevresult can be the result given by a previous call to UnitChanged (default is '0')

        returns '0' if name is null OR name.TEXT does not exist
        returns '3' if prevresult is '3' OR object filename doesn't exist OR interface changed OR
                        lastobj.OBJ is newer than name.OBJ
        returns '2' any implementation file has changed
        otherwise '1' }
DEFAULT prevresult TO '0'

IF name = '' THEN
    RETURN '0'
ENDIF

IF NOT (Exists("[name].TEXT")) THEN
    RETURN '0'
ENDIF

IF prevresult = '3' THEN
    RETURN '3'
ENDIF

IF NOT (Exists("[name].OBJ")) THEN
    RETURN '3'
ENDIF

IF lastobj <> '' THEN
    IF Exists("[lastobj].OBJ") THEN
        IF Newer("[lastobj].OBJ","[name].OBJ") THEN
            RETURN '3'
        ENDIF
    ENDIF
ENDIF

IF Newer("[name].TEXT","[name].OBJ") THEN
    RETURN '3'
ENDIF

SET ctr TO '2'
SET done TO 'F'
WHILE done = 'F' DO
    IF NOT (Exists("[name][ctr].TEXT")) THEN
        SET done TO 'T'
    ENDIF
    IF done = 'F' THEN
        IF Newer("[name][ctr].TEXT","[name].OBJ") THEN
            RETURN '2'
        ENDIF
    ENDIF
    SET ctr TO Eval(ctr + 1)
ENDWHILE

RETURN '1'
ENDEXEC

ÿ