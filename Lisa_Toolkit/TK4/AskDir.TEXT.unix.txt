EXEC(prompt,ln,temp,default)
        { Given a prompt such as 'Enter directory to which to copy sources', this
            exec file returns a directory as '-<name>-' or '' (means the prefix volume);
            the <name> that is entered is   used as the default the next time this is run. }

    { Set up the prompt string & default (if any) }
RESET COMMBUFR, 'TK Install'
READLN (COMMBUFR) default
IF (default <> '') AND (default <> 'EOF') THEN
    SET temp TO CONCAT('[', default, ']')
ELSE
    SET temp TO '[prefix volume]'
    SET default TO ''
ENDIF
SET prompt TO "[prompt] [temp] "

WHILE TRUE DO
    REQUEST ln WITH prompt
    SET ln TO UPPERCASE(TRIMBLANKS(ln))

        { Put the input into the proper form }
    IF (ln = '#PREFIX') OR (ln = '-#PREFIX') THEN
        SET ln TO ''
    ELSEIF COPY(ln,1,1)='-' THEN
        SET ln TO "[ln]-"
    ELSEIF ln = '' THEN
        IF default <> '' THEN
            SET ln TO "-[default]-"
        ELSE
            SET ln TO ''
        ENDIF
    ELSE
        SET ln TO "-[ln]-"
    ENDIF

           { ln now is of the form '-<name>-' or ''}
    IF temp = '' THEN
        RETURN temp
    ENDIF
    SET temp TO COPY(ln,1,Length(ln)-1)
    IF Exists(temp) THEN
        REWRITE COMMBUFR, 'TK Install'
        WRITELN (COMMBUFR) COPY(ln,2,Length(ln)-2)

        RETURN ln
    ENDIF

    WRITELN "The directory '[temp]' does not exist."
ENDWHILE

ENDEXEC

ÿ